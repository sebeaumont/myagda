#+TITLE: Natural numbers in Agda
#+AUTHOR: Simon Beaumont
#+EMAIL: datalligator@icloud.com

#+BIBLIOGRAPHY: ~/Notes/bibliography.bib
#+STARTUP: inlineimages overview latexpreview
#+LATEX_HEADER: \usepackage{tikz}
#+LATEX_HEADER: \usetikzlibrary{positioning}

* Natural Numbers

** Define Peano's numbers
#+begin_src agda2
module Naturals where

data ℕ : Set where
  zero : ℕ
  suc  : ℕ → ℕ

-- So we can use decimal notation in Agda (used for shorthand in identities)
{-# BUILTIN NATURAL ℕ #-}

-- We use a DIY Equality module for now to show how the tools for step
-- by step equational reasoning work.
-- otherwise: import Relation.Binary.PropositionalEquality as Eq
import Equality as Eq 
open Eq using (_≡_; refl; cong; sym)
open Eq.≡-Reasoning using (begin_; _≡⟨⟩_; step-≡; _∎)
#+end_src


** The additive monoid (ℕ,+,0)
#+begin_src agda2

-- Definition 1. Inductive definition of addition 
_+_ : ℕ → ℕ → ℕ
zero    + y = y
(suc x) + y = suc (x + y)

infixl 6 _+_

-- Theorem 1. Associativity of addition
+-assoc : ∀ (n m k : ℕ) → (n + m) + k ≡ n + (m + k) -- the induction hypothesis
+-assoc zero m k =
  begin
    (zero + m) + k
  ≡⟨⟩
    m + k
  ≡⟨⟩
    zero + (m + k)
  ∎
+-assoc (suc n) m k =
  begin
    (suc n + m) + k             -- using inductive defintion of addition
  ≡⟨⟩
    suc (n + m) + k
  ≡⟨⟩
    suc ((n + m) + k)           -- work down simplifying
  ≡⟨ cong suc (+-assoc n m k) ⟩ -- this step needs some explanation: [1]
    suc (n + (m + k))           -- work up from goal simplifying
  ≡⟨⟩
    suc n + (m + k)             -- goal
  ∎

-- [1] A relation is said to be a congruence relative to a given
-- function if it is preserved by application of the function. The
-- recursive application of the function (+-assoc n m k) has the type
-- of the induction hypotheis and `cong suc` prefaces `suc` to each
-- side of the equation; establishing evidence for the hypotheis.
-- See: plfa Ch 2. Induction.lagda.md

-- Lemma 1.1 additive identity of 0
+-identity⃗ : ∀ (n : ℕ) → (n + zero) ≡ n
+-identity⃗ zero = refl
+-identity⃗ (suc n) = cong suc (+-identity⃗ n)

+-identity⃐ : ∀ (n : ℕ) → zero + n ≡ n
+-identity⃐ zero = refl
+-identity⃐ (suc n) = cong suc (+-identity⃐ n)

-- Lemma 1.2 define suc on second argument of addition
+-suc : ∀ (m n : ℕ) → m + suc n ≡ suc (m + n)
+-suc zero n =
  begin
    zero + suc n
  ≡⟨⟩
    suc n
  ≡⟨⟩
    suc (zero + n)
  ∎
+-suc (suc m) n =
  begin
    suc m + suc n
  ≡⟨⟩
    suc (m + suc n)
  ≡⟨ cong suc (+-suc m n) ⟩
    suc (suc (m + n))
  ≡⟨⟩
    suc (suc m + n)
  ∎

-- Theorem 2 commutativity of addition
+-comm : ∀ (m n : ℕ) → m + n ≡ n + m
+-comm m zero =
  begin
    m + zero
  ≡⟨ +-identity⃗ m ⟩
    m
  ≡⟨⟩
    (zero + m)
  ∎
+-comm m (suc n) =
  begin
    m + suc n
  ≡⟨ +-suc m n ⟩
    suc (m + n)
  ≡⟨ cong suc (+-comm m n) ⟩
    suc n + m
  ∎

-- Corollary 1. rearragement of parenthesis (+ is left associative)
-- no induction just apply +-assoc twice to shift parens R and L
+-rearrange : ∀ (m n p q : ℕ) → (m + n) + (p + q) ≡ m + (n + p) + q 
+-rearrange m n p q =
  begin
    (m + n) + (p + q)
  ≡⟨ sym (+-assoc (m + n) p q) ⟩       -- shift parens from R to L
    ((m + n) + p) + q
  ≡⟨ cong (_+ q) (+-assoc m n p) ⟩     -- shift parens from L to R
    (m + (n + p)) + q
  ∎
#+end_src


** The mutiplicative monoid (ℕ,*,1)

*** Defintion 2. inductive definition of multiplication
#+begin_src agda2
_*_ : ℕ → ℕ → ℕ
zero    * _  = zero
(suc n) * m  = m + (n * m) 
#+end_src

*** Lemma 2.1 right multiplicative identity
#+begin_src agda2
*-identity⃗ : ∀ (n : ℕ) → n * 1 ≡ n
*-identity⃗ zero = refl
*-identity⃗ (suc n) =
  begin
    (suc n) * 1
  ≡⟨⟩
    suc (n * 1)
  ≡⟨ cong suc (*-identity⃗ n) ⟩
    suc n
  ∎
#+end_src

*** Lemma 2.2 left multiplicative identity
#+begin_src agda2
*-identity⃐ : ∀ (n : ℕ) → 1 * n ≡ n
*-identity⃐ zero = refl
*-identity⃐ (suc n) =
  begin
   1 * (suc n) 
  ≡⟨⟩
    suc (n + zero)              -- computer found this for me!
  ≡⟨ cong suc (*-identity⃐ n) ⟩
    suc n
  ∎
#+end_src

*** Theorem 2. multiplication distributes over addition
#+begin_src agda2
*-distrib-+ : ∀ (m n p : ℕ) -> (m + n) * p ≡ m * p + n * p
*-distrib-+ zero n p =
  begin
    (zero + n) * p
  ≡⟨⟩
     n * p
  ≡⟨⟩
  zero * p + n * p
  ∎
*-distrib-+ (suc m) n p =
  begin
    ((suc m) + n) * p
  ≡⟨ cong (_* p) (+-comm (suc m) n) ⟩  -- commutativity of +
    (n + (suc m)) * p
  ≡⟨ cong (_* p) (+-suc n m) ⟩         -- suc defined on 2nd arg
    (suc (n + m)) * p
  ≡⟨⟩                                  -- inductive defintion of *
    p + ((n + m) * p)           
  ≡⟨ cong (p +_) (*-distrib-+ n m p) ⟩ -- inductive case of *-distrib-+
    p + (n * p + m * p)         
  ≡⟨ cong (p +_) (+-comm (n * p) (m * p)) ⟩ -- commutativity of +
    p + (m * p + n * p)         
  ≡⟨ sym (+-assoc p (m * p) (n * p)) ⟩ -- move parens L 
    (p + (m * p)) + n * p       
  ≡⟨⟩                                  -- ^ inductive defintion of *
    (suc m) * p + n * p         
  ∎
#+end_src

*** Theorem 3. Associativity of multiplication
#+begin_src agda2
*-assoc : ∀ (n m l : ℕ) -> (n * m) * l ≡ n * (m * l)
*-assoc zero _ _ = refl
*-assoc (suc n) m l =
  begin
    ((suc n) * m) * l                  
  ≡⟨⟩                                  -- inductive defintion of *
    (m + n * m) * l                    
  ≡⟨ *-distrib-+ m (n * m) l ⟩         -- * distributes over +
    (m * l) + (n * m) * l
  ≡⟨ cong ((m * l) +_) (*-assoc n m l) ⟩ -- inductive case of *-assoc
    (m * l) + n * (m * l)
  ≡⟨⟩                                 -- ^ inductive defintion of *
    suc n * (m * l)
  ∎
#+end_src


** Using ~rewrite~ for shorthand equational reasoning

[[id:80C26F92-669C-48C5-8247-F9EF51EC331B][Agda Notes]]

#+begin_src agda2
*-assoc' : ∀ (n m l : ℕ) -> (n * m) * l ≡ n * (m * l)
*-assoc' zero _ _ = refl
*-assoc' (suc n) m l rewrite *-distrib-+ m (n * m) l | *-assoc n m l = refl

+-assoc' : ∀ (n m l : ℕ) -> (n + m) + l ≡ n + (m + l)
+-assoc' zero _ _ = refl
+-assoc' (suc n) m l rewrite +-assoc n m l = refl
#+end_src

** A Finitist proof of associativty of addition
Is this is cheating using refl?

#+begin_src agda2
finite-+-assoc : ∀ (n m k : ℕ) → (n + m) + k ≡ n + (m + k)
-- on day 1 we know 0 and (0 + 0) + 0 ≡ 0 + (0 + 0)
finite-+-assoc 0 0 0 = refl
-- on day 2 we know 1 and ...
finite-+-assoc 0 0 1 = refl
finite-+-assoc 0 1 0 = refl
finite-+-assoc 0 1 1 = refl
finite-+-assoc 1 0 0 = refl
finite-+-assoc 1 0 1 = refl
finite-+-assoc 1 1 0 = refl
finite-+-assoc 1 1 1 = refl
-- and then I gave up... but Agda gave me a clue...
finite-+-assoc zero zero (suc k) = refl
finite-+-assoc zero (suc m) k = refl
finite-+-assoc (suc n) m k rewrite finite-+-assoc n m k = refl
#+end_src


** Even and Odd Naturals
*** Definitions

Nota: forward declaration of mutually recursive types.
#+begin_src agda2
data even : ℕ → Set
data odd : ℕ → Set

data even where

  zero :
    ---------
    even zero
    
  suc : ∀ {n : ℕ}
    → odd n
      ------
    → even (suc n)
    
data odd where

  suc : ∀ {n : ℕ}
    → even n
      -----------
    → odd (suc n)
#+end_src

*** Prop 1: The sum of two even numbers is even
#+begin_src agda2
e+e≡e : ∀ {m n : ℕ}
  → even m
  → even n
    ------------
  → even (m + n)
#+end_src

*** Prop 2: The sum of an even and odd number is odd
#+begin_src agda2
o+e≡o : ∀ {m n : ℕ}
  → odd m
  → even n
    -----------
  → odd (m + n)
#+end_src

*** Proofs of Prop 1 and 2 (mutually recursive)
#+begin_src agda2
e+e≡e zero     en = en
e+e≡e (suc om) en = suc (o+e≡o om en)

o+e≡o (suc em) en = suc (e+e≡e em en)
#+end_src

*** Prop 3: The sum of two odd numbers is even
#+begin_src agda2
o+o≡e : ∀ {m n : ℕ}
  → odd m
  → odd n
  --------------
  → even (m + n)

o+o≡e (suc zero) on = suc on
o+o≡e (suc (suc om)) on = suc (suc (o+o≡e om on))
#+end_src
