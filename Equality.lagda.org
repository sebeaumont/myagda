#+TITLE: Propositional Equality in Agda
#+AUTHOR: Simon Beaumont
#+EMAIL: datalligator@icloud.com
#+BIBLIOGRAPHY: ~/Notes/bibliography.bib
#+STARTUP: inlineimages overview latexpreview
#+LATEX_HEADER: \usepackage{tikz}
#+LATEX_HEADER: \usetikzlibrary{positioning}

#+begin_src agda2

module Equality where

-- The identity type
data _≡_ {A : Set} : A → A → Set where 
  refl : {x : A} → x ≡ x
  
infix 4 _≡_

{-# BUILTIN EQUALITY _≡_ #-}

-- symmetry of equality
sym : {A : Set} {x y : A} → x ≡ y → y ≡ x
sym refl = refl

-- transitivity of equality
trans : {A : Set} {x y z : A} → x ≡ y → y ≡ z → x ≡ z
trans refl refl = refl

-- congruence of equality
cong : {A B : Set} {x y : A} → (f : A → B) → x ≡ y → f x ≡ f y
cong f refl = refl

--------------------------------------------------------------------------------
-- equational reasoning utilities - stolen from Core.agda to show how simple
-- this magic is ;-)

module ≡-Reasoning {A : Set} where
  
  infix  3 _∎
  infixr 2 _≡⟨⟩_ step-≡ step-≡˘
  infix  1 begin_

  begin_ : ∀ {x y : A} → x ≡ y → x ≡ y
  begin_ x≡y = x≡y

  _≡⟨⟩_ : ∀ (x {y} : A) → x ≡ y → x ≡ y
  _ ≡⟨⟩ x≡y = x≡y

  step-≡ : ∀ (x {y z} : A) → y ≡ z → x ≡ y → x ≡ z
  step-≡ _ y≡z x≡y = trans x≡y y≡z

  step-≡˘ : ∀ (x {y z} : A) → y ≡ z → y ≡ x → x ≡ z
  step-≡˘ _ y≡z y≡x = trans (sym y≡x) y≡z

  _∎ : ∀ (x : A) → x ≡ x
  _∎ _ = refl

  syntax step-≡  x y≡z x≡y = x ≡⟨  x≡y ⟩ y≡z
  syntax step-≡˘ x y≡z y≡x = x ≡˘⟨ y≡x ⟩ y≡z

#+end_src

